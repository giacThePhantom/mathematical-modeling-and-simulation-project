# ML with stochastic potassium channels
# uses the exact random time change algorithm

# membrane potential
V'=( I - gca*minf*(V-Vca)-gk*wtot*(V-VK)-gl*(V-Vl))/c
# unit exponentials
t[3..4]'=0
# number of potassium(w) /calcium (m) channels open
w'=0
# int_0^t beta(V(s)) ds
# for the 4 reactions
awp'=aw
bwp'=bw
# initialize unit exponentials
t[3..4](0)=-log(ran(1))
# look for crossings, reset integrals, increment channels, choose next time
global 1 awp-t3 {t3=-log(ran(1));awp=0;w=w+1}
global 1 bwp-t4 {t4=-log(ran(1));bwp=0;w=w-1}
# parameters
par Nw=100
init v=-50
# fraction of open channels
wtot=w/Nw
# ML channel kinetic definitions
minf=.5*(1+tanh((v-va)/vb))
winf=.5*(1+tanh((v-vc)/vd))
tauw=1/cosh((v-vc)/(2*vd))
alw=winf/tauw
blw=1/tauw-alw
# independent, so rates are just multiples of number in each state
aw=alw*(Nw-w)*phi
bw=blw*w*phi
param vk=-84,vl=-60,vca=120
param i=100,gk=8,gl=2,c=20,phim=.4
param va=-1.2,vb=18
# Uncomment the ones you like!!
par1-3 vc=2,vd=30,phi=.04,gca=4.4
set hopf {vc=2,vd=30,phi=.04,gca=4.4}
set snic  {vc=12,vd=17.4,phi=.06666667,gca=4}
set homo {vc=12,vd=17.4,phi=.23,gca=4}
# set up some numerics and plotting stuff
@ dt=.01,nout=100,total=4000,bound=1000
@ maxstor=5000,meth=euler
@ xhi=4000,ylo=-65,yhi=50
done
